plugins {
    id 'groovy'
    id 'application'
}

def klassName = 'io.github.mcolletta.mircomposer.MirComposer'

application {
    mainClass = klassName
}


dependencies {
    implementation project(':mirchord')
    implementation project(':mirconverter')
    implementation project(':mirscore')
    implementation project(':mirtextfx')
    implementation project(':mirmidi')
    implementation project(':mirsynth')
    implementation project(':mirutils')
    implementation project(':mirfoldertreeview')
    implementation project(':mirtrainer')
    implementation name: 'richtextfx-fat-0.11.6'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy']
        }
        resources {
            srcDirs = ["src/main/groovy"]
        }
    }
}

def javaHome = System.getenv('JAVA_HOME')

task copyModules(type: Copy) {
    from(configurations.runtimeClasspath).include("javafx-*.jar")
    into("$buildDir/modules")
}

task runMirComposer(type: JavaExec) {
    dependsOn build, copyModules
    description 'Run MirComposer'
    mainClass = klassName
    classpath = sourceSets.main.runtimeClasspath

    jvmArgs = [
        "--module-path", "$buildDir/modules${File.pathSeparator}$buildDir/libs",
        //"--add-modules=javafx.graphics,javafx.fxml,javafx.swing,javafx.media,javafx.web",
        "--add-modules=javafx.graphics,javafx.fxml,javafx.swing,javafx.media",

        "--add-exports", "javafx.base/com.sun.javafx=ALL-UNNAMED",
        "--add-exports", "javafx.base/com.sun.javafx.event=ALL-UNNAMED",
        "--add-exports", "javafx.base/com.sun.javafx.collections=ALL-UNNAMED",
        "--add-exports", "javafx.base/com.sun.javafx.binding=ALL-UNNAMED",
        "--add-exports", "javafx.base/com.sun.javafx.logging=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.application=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED",
        // "--add-exports", "javafx.graphics/com.sun.javafx.tk.quantum=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.scene.text=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.scene.input=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.css=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.geom=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.scene.layout=ALL-UNNAMED",
        "--add-exports", "javafx.graphics/com.sun.javafx.util=ALL-UNNAMED",
        "--add-exports", "java.desktop/com.sun.media.sound=ALL-UNNAMED",
    ]

    if (project.hasProperty('custom_arg')) {
        args project.getProperty('custom_arg')
    }
}

defaultTasks 'runMirComposer'

